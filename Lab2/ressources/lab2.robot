*** Settings ***

Library    SeleniumLibrary

Variables    ../pageobject/locators.py
Variables    ../pageobject/variables.py

*** Keywords ***
Open web browser
    Open Browser    ${url}    ${BROWSER}
    Maximize Browser Window
    Set Selenium Timeout   ${TIMEOUT}
    Set Selenium Speed    ${SPEED}
    Log To Console    Browser opened and configured successfully.

Close web browser
    Log To Console    Closing browser.
    Close Browser
    Close All Browsers

Navigate To Home Page
    Go To    ${URL}
    Log To Console    Navigation to home page complete.

User Examines Home Page Contents
    Page Should Contain Element         ${PAGE_TITLE}
    Element Text Should Be              ${PAGE_TITLE}    ${HOME_PAGE_TITLE}
    Log To Console    Home page contents verified.

Click Login Link
    Click Element    ${SIGN_IN_LINK}
    Wait Until Page Contains    ${LOGIN_PAGE_TITLE}
    Page Should Contain    ${LOGIN_PAGE_TITLE}
    Log To Console    Login page loaded successfully.

Type Valid Login Credentials
    Input Text    ${EMAIL_INPUT}    ${VALID_EMAIL}
    Input Text    ${PASSWORD_INPUT}    ${VALID_PASSWORD}
    Log To Console    Valid credentials entered: ${VALID_EMAIL}

Click Submit Button
    Click Element    ${SUBMIT_BUTTON}
    Wait Until Page Contains    ${CUSTOMER_PAGE_TITLE}
    Page Should Contain    ${CUSTOMER_PAGE_TITLE}
    Log To Console    Customers page loaded successfully.

Skip Login Credentials
    Log To Console    Skipping credential entry (leaving fields empty).

Click Submit Button Without Credentials
    Click Element    ${SUBMIT_BUTTON}
    Wait Until Page Contains    ${LOGIN_PAGE_TITLE}
    Page Should Contain    ${LOGIN_PAGE_TITLE}
    Log To Console    User remains on login page - login failed as expected.

Click Remember Me Checkbox
    Click Element    ${REMEMBER_ME_CHECKBOX}
    Checkbox Should Be Selected    ${REMEMBER_ME_CHECKBOX}
    Log To Console    Remember Me checkbox selected.

Click Logout Link
    Click Element    ${SIGN_OUT_LINK}
    Wait Until Page Contains    ${SIGNED_OUT_PAGE_TITLE}
    Page Should Contain    ${SIGNED_OUT_PAGE_TITLE}
    Log To Console    Logged out successfully, signed out page displayed.

Verify Email Is Pre-populated
    # ${email_value}=    Get Value    ${EMAIL_INPUT}
    # Should Be Equal    ${email_value}    ${VALID_EMAIL} (ne correpond pas Ã  la variable mais il est vide)
    # Log To Console    Email field is correctly pre-populated with: ${email_value}
    Should Be Empty    ${EMAIL_INPUT}
    Log To Console    Email field is not pre-populated, as expected.

Examine Customers Grid For Multiple Records
    ${row_count}=    Get Element Count    ${CUSTOMERS_TABLE_ROWS}
    Should Be True    ${row_count} > 1
    Log To Console    Customers grid contains ${row_count} records (more than 1 as expected).

Click New Customer Button
    Click Element    ${NEW_CUSTOMER_BUTTON}
    Wait Until Page Contains    ${ADD_CUSTOMER_PAGE_TITLE}
    Page Should Contain    ${ADD_CUSTOMER_PAGE_TITLE}
    Log To Console    Add Customer page loaded successfully.

Type Customer Email
    Input Text    ${CUSTOMER_EMAIL_INPUT}    ${CUSTOMER_EMAIL}
    Log To Console    Customer email entered: ${CUSTOMER_EMAIL}

Type Customer First Name
    Input Text    ${CUSTOMER_FIRST_NAME_INPUT}    ${CUSTOMER_FIRST_NAME}
    Log To Console    Customer first name entered: ${CUSTOMER_FIRST_NAME}

Type Customer Last Name
    Input Text    ${CUSTOMER_LAST_NAME_INPUT}    ${CUSTOMER_LAST_NAME}
    Log To Console    Customer last name entered: ${CUSTOMER_LAST_NAME}

Type Customer City
    Input Text    ${CUSTOMER_CITY_INPUT}    ${CUSTOMER_CITY}
    Log To Console    Customer city entered: ${CUSTOMER_CITY}

Select Customer State
    Select From List By Value    ${CUSTOMER_STATE_SELECT}    ${CUSTOMER_STATE}
    Log To Console    Customer state selected: ${CUSTOMER_STATE}

Select Gender
    Click Element    ${CUSTOMER_GENDER_MALE_RADIO}
    Log To Console    Gender selected: Male

Optionally Check Promotion Checkbox
    Click Element    ${CUSTOMER_PROMO_CHECKBOX}
    Checkbox Should Be Selected    ${CUSTOMER_PROMO_CHECKBOX}
    Log To Console    Promotional list checkbox selected.

Click Customer Submit Button
    Click Element    ${CUSTOMER_SUBMIT_BUTTON}
    Wait Until Page Contains Element    ${SUCCESS_MESSAGE}
    Page Should Contain    ${SUCCESS_MESSAGE_TEXT}
    Log To Console    Customer success page loaded with success message.

Click Cancel Button
    Click Element    ${CUSTOMER_CANCEL_BUTTON}
    Wait Until Page Contains    ${CUSTOMER_PAGE_TITLE}
    Page Should Contain    ${CUSTOMER_PAGE_TITLE}
    Log To Console    Returned to Customers page after clicking Cancel.
